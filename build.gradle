buildscript {
	repositories {
		maven { url 'https://maven.minecraftforge.net' }
		maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7.+'
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'com.modrinth.minotaur' version '2.+'
	id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.71.8'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

version = "${minecraft_version}-${mod_version}"
group = "${mod_base_package}"
archivesBaseName = "${mod_id}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

configurations {
	shade
}

minecraft {
	mappings channel: 'official', version: "${minecraft_version}"
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			taskName 'Client'
			workingDirectory file('run/client')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		if(project.hasProperty('mc_uuid') & project.hasProperty('mc_username')) {
			clientAuthed {
				parent runs.client
				taskName 'Client-Authed'
				args '--uuid', project.getProperty('mc_uuid'), '--username', project.getProperty('mc_username')
			}
		}

		server {
			taskName 'Server'
			workingDirectory file('run/server')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			args 'nogui'

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		data {
			taskName 'Data'
			workingDirectory file('run/client')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			args '--mod', mod_id, '--client', '--server', '--validate', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', apexcore_mod_id
			// ForgeGradle will just force-exit the Gradle Daemon which fails our builds in case
			// a daemon is used for any reason.
			forceExit false

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}
	}
}

if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
	mixin {
		add sourceSets.main, "${mod_id}.refmap.json"
		config "${mod_id}.mixins.json"
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://dvs1.progwml6.com/files/maven' }
	maven { url 'https://maven.tterrag.com/' }
	maven { url 'https://maven.apexmods.xyz/' }

	maven {
		url 'https://cursemaven.com'

		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

	compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
	runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

	compileOnly fg.deobf("team.chisel.ctm:CTM:${minecraft_version}-${ctm_version}:api")
	runtimeOnly fg.deobf("team.chisel.ctm:CTM:${minecraft_version}-${ctm_version}")

	implementation fg.deobf("${registrate_package}:registrate-apex:MC${minecraft_version}-${registrate_version}")
	shade "${registrate_package}:registrate-apex:MC${minecraft_version}-${registrate_version}"

	implementation fg.deobf("${mod_base_package}:${commonality_mod_id}:${minecraft_version}-${commonality_version}")
	shade "${mod_base_package}:${commonality_mod_id}:${minecraft_version}-${commonality_version}"

	implementation fg.deobf("${mod_base_package}:${apexcore_mod_id}:${minecraft_version}-${apexcore_version}:slim")
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allJava
	from sourceSets.main.resources
}

task deobfJar(type: Jar) {
	classifier 'deobf'
	exclude '**/*bbmodel'
	from sourceSets.main.output
}

jar {
	classifier 'slim'
	exclude '**/*bbmodel'
	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir
}

shadowJar {
	classifier ''
	configurations = [ project.configurations.shade ]
	relocate "${registrate_package}", "xyz.apex.repack.${registrate_package}"
	exclude '**/*bbmodel'

	// we only want to remap class refrences imports etc
	// and not shade in these into the mod jar
	// registrator already comes with these pre shaded
	// and we dependend on registrator
	dependencies {
		exclude dependency("${registrate_package}:registrate-apex:MC${minecraft_version}-${registrate_version}")
		exclude dependency("${mod_base_package}:${commonality_mod_id}:${minecraft_version}-${commonality_version}")
	}
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
	archives shadowJar
}

if(System.getenv('GENERATE_CHANGELOG') != null) {
	def previousGitTag
	def prefix = 'v'
	def suffix = ''

	if(mod_release_type == 'prerelease') {
		prefix = 'pv'
	} else if(mod_release_type == 'alpha' || mod_release_type == 'beta') {
		suffix = "${mod_release_type}".toUpperCase()
	}

	def currentGitTag = "${prefix}${mod_version}${suffix}"

	try {
		def code = new ByteArrayOutputStream()

		exec {
			commandLine 'git', 'describe', '--abbrev=0', '--tags', '--exclude', currentGitTag
			standardOutput code
		}

		previousGitTag = code.toString().trim()
	} catch(e) {
		previousGitTag = ''
	}

	task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
		file = file("CHANGELOG.md")
		fromRepo = file('.')
		fromRef = "${previousGitTag}"
		toRef = "v${mod_version}"
		templateContent = file('.github/changelog.mustache').text
		                                                    .replace('{current_tag}', "${currentGitTag}")
		                                                    .replace('{mod_git_url}', "${mod_git_url}")
		                                                    .replace('{last_tag}', "${previousGitTag}")
	}
}

tasks.withType(Jar) {
	from file('CHANGELOG.md')
	from file('LICENSE')

	manifest {
		attributes([
				'Specification-Title': "${mod_name}",
				'Specification-Vendor': "${mod_authors}",
				'Specification-Version': "${mod_version}",
				'Implementation-Title': "${mod_name}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : "${mod_authors}",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
		manifest {
			attributes 'MixinConfigs': "${mod_id}.mixins.json"
		}
	}
}

reobf {
	shadowJar { }
}

jar.finalizedBy('reobfJar')
shadowJar.finalizedBy('reobfShadowJar')

if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = "${mod_base_package}"
				artifactId = "${mod_id}"
				version = "${minecraft_version}-${mod_version}"

				artifact jar
				artifact sourcesJar
				artifact deobfJar
				artifact shadowJar
			}
		}
		repositories {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
} else {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = "${mod_base_package}"
				artifactId = "${mod_id}"
				version = "${minecraft_version}-${mod_version}"
				artifact jar
				artifact sourcesJar
				artifact deobfJar
				artifact shadowJar
			}
		}
		repositories {
			mavenLocal()
		}
	}
}

if(System.getenv('CURSEFORGE_TOKEN') != null) {
	curseforge {
		apiKey = System.getenv('CURSEFORGE_TOKEN')

		project {
			id = "${mod_curseforge_id}"
			releaseType = "${mod_release_type}"
			addGameVersion "${minecraft_version}"
			changelogType = 'markdown'
			changelog = file('CHANGELOG.md').text

			relations {
				requiredDependency "${apexcore_mod_id}"
			}

			mainArtifact shadowJar
			addArtifact jar
			addArtifact sourcesJar
			addArtifact deobfJar
		}

		options {
			detectNewerJava = true
		}
	}
}

import com.modrinth.minotaur.dependencies.ModDependency

if(System.getenv('MODRINTH_TOKEN') != null) {
	modrinth {
		token = System.getenv('MODRINTH_TOKEN')
		projectId = "${mod_modrinth_id}"
		versionNumber = "${mod_version}"
		versionName = "${mod_name}-${minecraft_version}-${mod_version}"
		versionType = "${mod_release_type}"
		uploadFile = shadowJar
		additionalFiles = [ jar, sourcesJar, deobfJar ]
		gameVersions = [ "${minecraft_version}" ]
		changelog = file('CHANGELOG.md').text
		loaders = [ 'forge' ]
		dependencies = [
				new ModDependency('xl3myxch', 'required') // ApexCore
		]
	}
}