buildscript {
	repositories {
		maven { url 'https://maven.minecraftforge.net' }
		maven { url 'https://maven.parchmentmc.org' }
		maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forge_gradle_version}", changing: true
		classpath "org.parchmentmc:librarian:${librarian_version}"
		classpath "org.spongepowered:mixingradle:${mixin_gradle_version}"
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version "${gradle_shadow_version}"
	id 'com.matthewprenger.cursegradle' version "${cursegradle_version}"
	id 'com.modrinth.minotaur' version "${minotaur_version}"
	id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version "${gradle_changelog_version}"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

version = "${minecraft_version}-${mod_version}"
group = "${mod_base_package}.${mod_id}"
archivesBaseName = "${mod_id}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
	shade
}

minecraft {
	mappings channel: "${mappings_channel}", version: "${mappings_version}"
	accessTransformer = file('accesstransformer.cfg')

	runs {
		client {
			taskName 'Client'
			workingDirectory file('run/client')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		if(project.hasProperty('mc_uuid') & project.hasProperty('mc_username')) {
			clientAuthed {
				parent runs.client
				taskName 'Client-Authed'
				args '--uuid', project.getProperty('mc_uuid'), '--username', project.getProperty('mc_username')
			}
		}

		server {
			taskName 'Server'
			workingDirectory file('run/server')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			args 'nogui'

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		data {
			taskName 'Data'
			workingDirectory file('run/client')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			args '--mod', mod_id, '--client', '--server', '--validate', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', registrator_mod_id, '--existing-mod', apexcore_mod_id
			// ForgeGradle will just force-exit the Gradle Daemon which fails our builds in case
			// a daemon is used for any reason.
			forceExit false

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}
	}
}

if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
	mixin {
		add sourceSets.main, "${mod_id}.refmap.json"
		config "${mod_id}.mixins.json"
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://dvs1.progwml6.com/files/maven' }
	maven { url 'https://maven.tterrag.com/' }
	maven { url 'https://maven.apexmods.xyz/' }
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	annotationProcessor "org.spongepowered:mixin:${mixin_ap_version}:processor"

	compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
	runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

	// CTM for 1.16.1 works for 1.16.5 (there is no 1.16.5 specific release)
	runtimeOnly fg.deobf("team.chisel.ctm:CTM:MC1.16.1-${ctm_version}")
	// runtimeOnly fg.deobf("team.chisel.ctm:CTM:MC${mc_version}-${ctm_version}")

	implementation fg.deobf("com.tterrag.registrate:Registrate:MC${minecraft_version}-${registrate_version}")
	shade "com.tterrag.registrate:Registrate:MC${minecraft_version}-${registrate_version}"

	implementation "xyz.apex.java.utility:javautilities:${javautilities_version}"
	shade "xyz.apex.java.utility:javautilities:${javautilities_version}"

	implementation fg.deobf("xyz.apex.forge.utility:${registrator_mod_id}:${minecraft_version}-${registrator_version}:deobf")
	implementation fg.deobf("xyz.apex.forge:${apexcore_mod_id}:${minecraft_version}-${apexcore_version}:deobf")
}

def resourceTargets = [ 'META-INF/mods.toml', '**/*.mcmeta', "${mod_id}.mixins.json".toString() ]
def intoTargets = [ "${projectDir}/out/production/resources/", "${projectDir}/out/production/${project.name}.main/", "${projectDir}/bin/main/" ]
def replaceProperties = project.properties

processResources {
	filesMatching(resourceTargets) {
		expand replaceProperties
	}

	intoTargets.each { target ->
		if(file(target).exists()) {
			copy{
				from(sourceSets.main.resources) {
					include resourceTargets
					expand replaceProperties
				}

				into target
			}
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allJava
	from sourceSets.main.resources
}

task deobfJar(type: Jar) {
	classifier 'deobf'
	from sourceSets.main.output
	exclude '**/*bbmodel'
}

jar {
	classifier 'slim'

	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir
	exclude '**/*bbmodel'
}

shadowJar {
	classifier ''
	configurations = [ project.configurations.shade ]
	relocate "${registrate_package}", "xyz.apex.repack.${registrate_package}"
	exclude '**/*bbmodel'

	// we only want to remap class refrences imports etc
	// and not shade in these into the mod jar
	// registrator already comes with these pre shaded
	// and we dependend on registrator
	dependencies {
		exclude dependency("com.tterrag.registrate:Registrate:MC${minecraft_version}-${registrate_version}")
		exclude dependency("xyz.apex.java.utility:javautilities:${javautilities_version}")
	}
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
	archives shadowJar
}

if(System.getenv('GENERATE_CHANGELOG') != null) {
    def previousGitTag = { ->
        def code = new ByteArrayOutputStream()

        exec {
            commandLine 'git', 'describe', '--tags', '--match', 'v*', '--exclude', "v${mod_version}"
            standardOutput code
        }

        return code.toString().trim().split('-')[0]
    }

	// def previousGitTag = ''

    task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
        file = file("CHANGELOG.md")
        fromRepo = file('.')
        fromRef = "${previousGitTag}-ALPHA" // TODO: Remove once we leave alpha
        toRef = "v${mod_version}"
        templateContent = file('.github/changelog.mustache').text
		        .replace('{mod_version}', "${mod_version}")
		        .replace('{mod_git_url}', "${mod_git_url}")
		        .replace('{last_tag}', "${previousGitTag}")
    }
}

tasks.withType(Jar) {
	from(file('accesstransformer.cfg')) {
		into '/META-INF'
	}

	from file('CHANGELOG.md')
	from file('LICENSE')

	manifest {
		attributes([
				'Specification-Title': "${mod_name}",
				'Specification-Vendor': "${mod_authors}",
				'Specification-Version': "${mod_version}",
				'Implementation-Title': "${mod_name}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : "${mod_authors}",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
		manifest {
			attributes 'MixinConfigs': "${mod_id}.mixins.json"
		}
	}
}

reobf {
	shadowJar { }
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar.finalizedBy('reobfJar')

if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = "${mod_base_package}"
				artifactId = "${mod_id}"
				version = "${minecraft_version}-${mod_version}"

				artifact jar
				artifact sourcesJar
				artifact deobfJar
				artifact shadowJar
			}
		}
		repositories {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
} else {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = "${mod_base_package}"
				artifactId = "${mod_id}"
				version = "${minecraft_version}-${mod_version}"
				artifact jar
				artifact sourcesJar
				artifact deobfJar
				artifact shadowJar
			}
		}
		repositories {
			mavenLocal()
		}
	}
}

if(System.getenv('CURSEFORGE_TOKEN') != null) {
	curseforge {
		apiKey = System.getenv('CURSEFORGE_TOKEN')

		project {
			id = "${mod_curseforge_id}"
			releaseType = "${mod_release_type}"
			addGameVersion "${minecraft_version}"
			changelogType = 'markdown'
			changelog = file('CHANGELOG.md').text

			relations {
				requiredDependency "${registrator_mod_id}"
				requiredDependency "${apexcore_mod_id}"
				optionalDependency "${jei_mod_id}"
				optionalDependency "${ctm_mod_id}"
			}

			mainArtifact shadowJar
			addArtifact jar
			addArtifact sourcesJar
			addArtifact deobfJar
		}

		options {
			detectNewerJava = true
		}
	}
}

import com.modrinth.minotaur.dependencies.ModDependency

if(System.getenv('MODRINTH_TOKEN') != null) {
	modrinth {
		token = System.getenv('MODRINTH_TOKEN')
		projectId = "${mod_modrinth_id}"
		versionNumber = "${mod_version}"
		versionName = "${mod_name}-${minecraft_version}-${mod_version}"
		versionType = "${mod_release_type}"
		uploadFile = shadowJar
		additionalFiles = [ jar, sourcesJar, deobfJar ]
		gameVersions = [ "${minecraft_version}" ]
		changelog = file('CHANGELOG.md').text
		loaders = [ 'forge' ]
		dependencies = [
				new ModDependency("${registrator_modrinth_slug}", 'required'),
				new ModDependency("${apexcore_modrinth_slug}", 'required')
		]
	}
}
