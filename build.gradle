plugins {
	id 'java'
	id 'idea'
	id 'maven-publish'

	id 'net.minecraftforge.gradle' version '5.1.+'
	id 'org.spongepowered.mixin' version '0.7.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
	id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1'
	id 'com.palantir.git-version' version '1.0.0'
	id 'com.modrinth.minotaur' version '2.+'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'net.minecraftforge.gradleutils' version '2.+'
}

afterEvaluate {
	tasks.setupTeamCityProject.enabled = false
	tasks.findByName('configureTeamCity')??.enabled = false
}

def repo = versionDetails()
version = "${repo.lastTag.substring(1)}.${repo.commitDistance}"
group = "${mod_base_package}"
archivesBaseName = "${mod_id}-${minecraft_version}"

jarJar.enable()

configurations {
	library
	implementation.extendsFrom library
}

sourceSets {
	main {
		java {
			srcDirs = [ 'src/main/java' ]
		}

		resources {
			srcDirs = [ 'src/main/resources', 'src/generated/resources' ]
		}
	}

	test {
		java {
			srcDirs = [ 'src/test/java' ]
		}

		resources {
			srcDirs = [ 'src/test/resources' ]
		}
	}
}

minecraft {
	mappings channel: 'parchment', version: "${parchment_mappings}-${minecraft_version}"
	// mappings channel: 'official', version: "${minecraft_version}"
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			taskName 'Client'
			workingDirectory file('run/client')
			property 'forge.logging.markers', ''
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			property 'forge.enableGameTest', 'true'
			property 'forge.enabledGameTestNamespaces', "${mod_id}"
			args '--width', 1920, '--height', 1080

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		if(project.hasProperty('mc_uuid') & project.hasProperty('mc_username')) {
			clientAuthed {
				parent runs.client
				taskName 'Client-Authed'
				args '--uuid', project.getProperty('mc_uuid'), '--username', project.getProperty('mc_username')
			}
		}

		server {
			taskName 'Server'
			workingDirectory file('run/server')
			property 'forge.logging.markers', ''
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			property 'forge.enableGameTest', 'true'
			property 'forge.enabledGameTestNamespaces', "${mod_id}"
			args 'nogui'

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		gameTestServer {
			taskName 'GameTest-Server'
			workingDirectory file('run/gametest-server')
			property 'forge.logging.markers', ''
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			property 'forge.enableGameTest', 'true'
			property 'forge.enabledGameTestNamespaces', "${mod_id}"
			args 'nogui'
			forceExit false

			mods {
				"${mod_id}" {
					source sourceSets.main
					source sourceSets.test
				}
			}
		}

		data {
			taskName 'Data'
			workingDirectory file('run/client')
			property 'forge.logging.markers', ''
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			args '--mod', mod_id, '--client', '--server', '--validate', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', apexcore_mod_id, '--existing-mod', commonality_mod_id
			// ForgeGradle will just force-exit the Gradle Daemon which fails our builds in case
			// a daemon is used for any reason.
			forceExit false

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}
	}
}

minecraft.runs.all {
	lazyToken('minecraft_classpath') {
		configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
	}
}

if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
	mixin {
		add sourceSets.main, "${mod_id}.refmap.json"
		config "${mod_id}.mixins.json"
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://dvs1.progwml6.com/files/maven' }
	maven { url 'https://maven.tterrag.com/' }
	maven { url 'https://maven.covers1624.net/' }
	maven { url 'https://maven.blamejared.com/' }

	maven {
		url 'https://cursemaven.com'

		content {
			includeGroup "curse.maven"
		}
	}

	maven {
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

	compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
	compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
	runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

	runtimeOnly fg.deobf("curse.maven:jade-${jade_id}:${jade_version}")

	// compileOnly fg.deobf("team.chisel.ctm:CTM:${minecraft_version}-${ctm_version}:api")
	// runtimeOnly fg.deobf("team.chisel.ctm:CTM:${minecraft_version}-${ctm_version}")

//	runtimeOnly fg.deobf("curse.maven:xycraft-653786:3960430") // XyCraft: Core - v0.5.2

	implementation fg.deobf("${registrate_package}:Registrate:MC${minecraft_version}-${registrate_version}")

	implementation fg.deobf("${mod_base_package}:${commonality_mod_id}-${minecraft_version}:${commonality_version}")
	implementation fg.deobf("${mod_base_package}:${apexcore_mod_id}-${minecraft_version}:${apexcore_version}:slim")
	jarJar(group: "${mod_base_package}", name: "${apexcore_mod_id}-${minecraft_version}", version: "${apexcore_version_range}")
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.release.set(JavaLanguageVersion.of("${java_version}").asInt())

	javaToolchains {
		compilerFor {
			languageVersion.set(JavaLanguageVersion.of("${java_version}"))
		}
	}
}

changelog {
	fromTag "${repo.lastTag}"
}

tasks.withType(Jar) {
	from tasks.createChangelog.outputFile
	from file('LICENSE')

	manifest {
		attributes([
				'Specification-Title': "${project.name}",
				'Specification-Vendor': 'ApexStudios',
				'Specification-Version': "${project.version}",
				'Implementation-Title': "${mod_name}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : 'ApexStudios',
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
		manifest {
			attributes 'MixinConfigs': "${mod_id}.mixins.json"
		}
	}
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of("${java_version}"))
	}

	withSourcesJar()
}

tasks.jarJar.configure {
	classifier ''
}

jar {
	classifier 'slim'
	from sourceSets.main.output
}

artifacts {
	archives jar
	archives sourcesJar
	archives tasks.jarJar
}

reobf {
	jarJar { }
}

jar.finalizedBy('reobfJar')
tasks.jarJar.finalizedBy('reobfJarJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "${project.group}"
			artifactId = "${mod_id}-${minecraft_version}"
			version = "${project.version}"

			artifact jar
			artifact sourcesJar
			artifact tasks.jarJar
		}
	}

	repositories {
		if(System.getenv('MAVEN_PASSWORD') != null) {
            maven {
                name 'Covers1624-Maven'
                url 'https://nexus.covers1624.net/repository/apex-releases/'

                credentials {
                    username 'apex'
                    password System.getenv('MAVEN_PASSWORD')
                }
            }
        }
	}
}

if(System.getenv('CURSEFORGE_TOKEN') != null) {
	curseforge {
		apiKey = System.getenv('CURSEFORGE_TOKEN')

		project {
			id = "${mod_curseforge_id}"
			releaseType = "${mod_release_type}"
			addGameVersion "${minecraft_version}"

			mainArtifact(tasks.jarJar) {
				changelog = tasks.createChangelog.outputFile
				embeddedLibrary 'apexcore'
				optionalDependency 'jei'
			}

			addArtifact(jar) {
				requiredDependency 'apexcore'
				optionalDependency 'jei'
			}

			addArtifact sourcesJar
		}

		options {
			debug = true
		}
	}
}

if(System.getenv('MODRINTH_TOKEN') != null) {
	modrinth {
		token = System.getenv('MODRINTH_TOKEN')
		projectId = "${mod_modrinth_id}"
		versionNumber = "${version}"
		versionType = "${mod_release_type}"
		versionName = "${mod_name} - v${version} for Minecraft ${minecraft_version}"
		changelog = tasks.createChangelog.outputFile.text
		uploadFile = tasks.jarJar
		additionalFiles = [ jar, sourcesJar ]
		gameVersions = [ "${minecraft_version}" ]
		loaders = [ 'forge' ]
		debugMode = true
		dependencies {
			embedded.project 'apexcore'
			optional.project 'jei'
		}
	}
}

idea.module {
	excludeDirs << file('run')
}
