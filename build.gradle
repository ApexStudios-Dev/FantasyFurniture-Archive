plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'

	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'com.modrinth.minotaur' version '2.+'
	id 'net.minecraftforge.gradle' version '5.1.+'
	id 'org.spongepowered.mixin' version '0.7.+'
}

def IS_EARLY_ACCESS = false
def USE_EARLY_ACCESS = false

version = "${mod_version}"
group = "${mod_base_package}"
archivesBaseName = IS_EARLY_ACCESS ? "${mod_id}-${minecraft_version}-EAP" : "${mod_id}-${minecraft_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

jarJar.enable()

configurations {
	library
	implementation.extendsFrom library
}

minecraft {
	mappings channel: 'official', version: "${minecraft_version}"
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			taskName 'Client'
			workingDirectory file('run/client')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			property 'forge.enableGameTest', 'true'
			property 'forge.enabledGameTestNamespaces', "${mod_id}"

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		if(project.hasProperty('mc_uuid') & project.hasProperty('mc_username')) {
			clientAuthed {
				parent runs.client
				taskName 'Client-Authed'
				args '--uuid', project.getProperty('mc_uuid'), '--username', project.getProperty('mc_username')
			}
		}

		server {
			taskName 'Server'
			workingDirectory file('run/server')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			property 'forge.enableGameTest', 'true'
			property 'forge.enabledGameTestNamespaces', "${mod_id}"
			args 'nogui'

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		gameTestServer {
			taskName 'GameTest-Server'
			workingDirectory file('run/gametest-server')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			property 'forge.enableGameTest', 'true'
			property 'forge.enabledGameTestNamespaces', "${mod_id}"
			args 'nogui'
			forceExit false

			mods {
				"${mod_id}" {
					source sourceSets.main
					source sourceSets.test
				}
			}
		}

		data {
			taskName 'Data'
			workingDirectory file('run/client')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			args '--mod', mod_id, '--client', '--server', '--validate', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', apexcore_mod_id, '--existing-mod', commonality_mod_id
			// ForgeGradle will just force-exit the Gradle Daemon which fails our builds in case
			// a daemon is used for any reason.
			forceExit false

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}
	}
}

minecraft.runs.all {
	lazyToken('minecraft_classpath') {
		configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
	}
}

if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
	mixin {
		add sourceSets.main, "${mod_id}.refmap.json"
		config "${mod_id}.mixins.json"
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://dvs1.progwml6.com/files/maven' }
	maven { url 'https://maven.tterrag.com/' }
	maven { url 'https://maven.apexstudios.dev/' }

	maven {
		url 'https://cursemaven.com'

		content {
			includeGroup "curse.maven"
		}
	}

	maven {
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

	compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
	compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
	runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

	// compileOnly fg.deobf("team.chisel.ctm:CTM:${minecraft_version}-${ctm_version}:api")
	// runtimeOnly fg.deobf("team.chisel.ctm:CTM:${minecraft_version}-${ctm_version}")

	implementation fg.deobf("${registrate_package}:Registrate:MC1.19-${registrate_version}") // TODO: Change back to ${minecraft_version} in 1.20

	if(USE_EARLY_ACCESS) {
		implementation fg.deobf("${mod_base_package}:${commonality_mod_id}-${minecraft_version}-EAP:${commonality_version}")
		implementation fg.deobf("${mod_base_package}:${apexcore_mod_id}-${minecraft_version}-EAP:${apexcore_version}:slim")
		jarJar(group: "${mod_base_package}", name: "${apexcore_mod_id}-${minecraft_version}-EAP", version: "${apexcore_version_range}")
	} else {
		implementation fg.deobf("${mod_base_package}:${commonality_mod_id}-${minecraft_version}:${commonality_version}")
		implementation fg.deobf("${mod_base_package}:${apexcore_mod_id}-${minecraft_version}:${apexcore_version}:slim")
		jarJar(group: "${mod_base_package}", name: "${apexcore_mod_id}-${minecraft_version}", version: "${apexcore_version_range}")
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allJava
	from sourceSets.main.resources
}

tasks.jarJar.configure {
	classifier ''
}

jar {
	classifier 'slim'
	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir
}

artifacts {
	archives jar
	archives sourcesJar
	archives tasks.jarJar
}

tasks.withType(Jar) {
	from file('.github/CHANGELOG.md')
	from file('LICENSE')

	manifest {
		attributes([
				'Specification-Title': "${mod_name}",
				'Specification-Vendor': "${mod_authors}",
				'Specification-Version': "${mod_version}",
				'Implementation-Title': "${mod_name}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : "${mod_authors}",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	if(file("./src/main/resources/${mod_id}.mixins.json").exists()) {
		manifest {
			attributes 'MixinConfigs': "${mod_id}.mixins.json"
		}
	}
}

jar.finalizedBy('reobfJar')
tasks.jarJar.finalizedBy('reobfJarJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "${mod_base_package}"
			artifactId = IS_EARLY_ACCESS ? "${mod_id}-${minecraft_version}-EAP" : "${mod_id}-${minecraft_version}"
			version = "${mod_version}"

			artifact jar
			artifact sourcesJar
			artifact tasks.jarJar
		}
	}
	repositories {
		mavenLocal()

		if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
}

if(System.getenv('CURSEFORGE_TOKEN') != null) {
	curseforge {
		apiKey = System.getenv('CURSEFORGE_TOKEN')

		project {
			id = "${mod_curseforge_id}"
			releaseType = "${mod_release_type}"
			addGameVersion "${minecraft_version}"
			changelogType = 'markdown'
			changelog = file('.github/CHANGELOG.md').text

			relations {
				embeddedLibrary "${apexcore_mod_id}"
			}

			mainArtifact tasks.jarJar
			addArtifact sourcesJar
			addArtifact jar
		}

		options {
			detectNewerJava = true
		}
	}
}

import com.modrinth.minotaur.dependencies.ModDependency

if(System.getenv('MODRINTH_TOKEN') != null) {
	modrinth {
		token = System.getenv('MODRINTH_TOKEN')
		projectId = "${mod_modrinth_id}"
		versionNumber = "${mod_version}"
		versionName = "${mod_name}-${minecraft_version}-${mod_version}"
		versionType = "${mod_release_type}"
		uploadFile = tasks.jarJar
		additionalFiles = [ jar, sourcesJar ]
		gameVersions = [ "${minecraft_version}" ]
		changelog = file('.github/CHANGELOG.md').text
		loaders = [ 'forge' ]
		dependencies = [
				new ModDependency('xl3myxch', 'optional') // ApexCore
		]
	}
}